# default header
import os
Import('proto_files_cc')

debug = int(ARGUMENTS.get('debug', 1))

cc_flags = ["-O3"]
if debug:
    cc_flags.append("-g")
    cc_flags.append("-DDEBUG")
else:
    cc_flags.append("-DNDEBUG")

env = Environment(
    CXX='clang++',
    CXXFLAGS='-std=c++14 -Wall -Wextra -msse -msse2 -msse3 -msse4.1 -mavx -mavx2 -mfma',
    CCFLAGS=cc_flags,
    CPPPATH=[
        '/usr/include/eigen3',
        '../',
        './',
    ],
    LIBPATH=[
    ],
    CPPDEFINES=[
    ]
    )
env['ENV']['TERM'] = os.environ.get('TERM', '')

# project specific code
LIBS = [
    'libboost_filesystem',
    'libboost_program_options',
    'libboost_system',
    'libboost_thread',
    'libdl',
    'libglog',
    'libopencv_core',
    'libopencv_features2d',
    'libopencv_highgui',
    'libopencv_imgproc',
    'libprotobuf',
    'libpthread',
]

source_files_wo_proto = (
    env.Glob('*.cpp') + env.Glob('*/*.cpp') + env.Glob("*/*.c"))

env.Program(
    'worker',
    source=[
        env.Object(f) for f
        in source_files_wo_proto + proto_files_cc
        if not f.name.endswith('_test.cpp')],
    LIBS=LIBS)

program_test = env.Program(
    'worker_test',
    source=[
        env.Object(f) for f
        in source_files_wo_proto + proto_files_cc
        if f.name != 'main.cpp'],
    LIBS=LIBS + ['libgtest', 'libgtest_main'])

env.Command('test', None, './' + program_test[0].path)
env.Depends('test', 'worker_test')
